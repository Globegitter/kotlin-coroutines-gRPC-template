apply plugin: 'kotlin'
apply plugin: 'com.google.protobuf'
apply plugin: 'idea'

dependencies {

    // For jdk 9+ you need to include javax.annotations
    // The reason is outlined in this grpc issue
    // https://github.com/grpc/grpc-java/issues/4725
    compileOnly "javax.annotation:javax.annotation-api:1.2"

    implementation "org.jetbrains.kotlin:kotlin-stdlib"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    
    implementation "io.grpc:grpc-stub:$grpc_version"
    implementation "io.grpc:grpc-protobuf-lite:$grpc_version"
    
    implementation ("com.github.marcoferrer.krotoplus:kroto-plus-coroutines:$krotoplus_version"){
        exclude group: 'io.grpc'
    }
}

protobuf {
    protoc { 
        artifact = "com.google.protobuf:protoc:$protobuf_version"
    }
    
    //noinspection GroovyAssignabilityCheck
    plugins {
        javalite { artifact = "com.google.protobuf:protoc-gen-javalite:3.0.0" }
        grpc { artifact = "io.grpc:protoc-gen-grpc-java:$grpc_version" }
        coroutines {
            artifact = "com.github.marcoferrer.krotoplus:protoc-gen-grpc-coroutines:$krotoplus_version:jvm8@jar"
        }
    }

    generateProtoTasks {

        all().each{ task ->
            task.builtins{
                remove java
            }
            task.plugins {
                javalite {}
                coroutines {}
                grpc { option 'lite' }
            }
        }
    }
}